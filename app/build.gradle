apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdk 29
    namespace "io.github.jokoframework.chake"
    testNamespace "io.github.jokoframework.chaketestapp"

    defaultConfig {
        applicationId "io.github.jokoframework.chake"
        minSdkVersion 29
        targetSdkVersion 29
        versionCode project.appVersion
        versionName "1.0.$project.appVersion"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        ndk.abiFilters 'armeabi-v7a','arm64-v8a','x86','x86_64'
        if (project.hasProperty('chake_username')) {
            buildConfigField "String", "CHAKE_USERNAME", "\"${chake_username}\""
        } else {
            buildConfigField "String", "CHAKE_USERNAME", "\"\""
        }
        if (project.hasProperty('chake_password')) {
            buildConfigField "String", "CHAKE_PASSWORD", "\"${chake_password}\""
        } else {
            buildConfigField "String", "CHAKE_PASSWORD", "\"\""
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        applicationVariants.all { variant ->
            variant.outputs.all { output ->
                def newApkName = applicationId + "-" + variant.versionName + "-v" + variant.versionCode + ".apk";
                outputFileName = new File(newApkName);
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'py.com.sodep.captura:form-definition:0.0.5'
    implementation 'py.com.sodep.captura:captura-exchange:0.0.11'
    implementation 'me.dm7.barcodescanner:zxing:1.9.8'
    implementation ('com.google.firebase:firebase-core:18.0.3') {
        exclude module: "play-services-ads-identifier"
    }
    implementation 'com.google.firebase:firebase-messaging:21.1.0'
    implementation 'com.google.firebase:firebase-crashlytics-ndk:17.4.1'
    implementation "com.fasterxml.jackson.core:jackson-core:$project.jacksonCoreVersion"
    implementation "com.fasterxml.jackson.core:jackson-databind:$project.jacksonDatabindVersion"
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

buildscript {

    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files\
        classpath 'org.owasp:dependency-check-gradle:5.3.2'
    }
}

apply plugin: 'org.owasp.dependencycheck'

dependencyCheck {
    autoUpdate = true
    cveValidForHours = 4
    failBuildOnCVSS = 30
    format = 'ALL'
}
