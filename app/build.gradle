apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdk 34
    namespace "io.github.jokoframework.chake"
    testNamespace "io.github.jokoframework.chaketestapp"

    defaultConfig {
        applicationId "io.github.jokoframework.chake"
        minSdkVersion 28
        targetSdkVersion 29
        versionCode project.appVersion
        versionName "1.0.$project.appVersion"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        ndk.abiFilters 'armeabi-v7a','arm64-v8a','x86','x86_64'
        def properties = new Properties()
        def configFile = rootProject.file("chake-config.properties")
        if (configFile.exists()) {
            configFile.withInputStream { stream ->
                properties.load(stream)
            }
        }

        def chakeUsername = properties.getProperty('chake_username', "")
        def chakePassword = properties.getProperty('chake_password', "")
        def chakeServerUrl = properties.getProperty('chake_server_url', "")
        def chakeMapUrl = properties.getProperty('chake_map_url', "")
        buildConfigField "String", "CHAKE_USERNAME", "\"${chakeUsername}\""
        buildConfigField "String", "CHAKE_PASSWORD", "\"${chakePassword}\""
        buildConfigField "String", "CHAKE_SERVER_URL", "\"${chakeServerUrl}\""
        buildConfigField "String", "CHAKE_MAP_URL", "\"${chakeMapUrl}\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        applicationVariants.all { variant ->
            variant.outputs.all { output ->
                def newApkName = applicationId + "-" + variant.versionName + "-v" + variant.versionCode + ".apk"
                outputFileName = new File(newApkName)
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'py.com.sodep.captura:form-definition:0.0.5'
    implementation 'py.com.sodep.captura:captura-exchange:0.0.11'
    implementation 'me.dm7.barcodescanner:zxing:1.9.8'
    implementation ('com.google.firebase:firebase-core:21.1.1') {
        exclude module: "play-services-ads-identifier"
    }
    implementation 'com.google.firebase:firebase-messaging:23.4.1'
    implementation 'com.google.firebase:firebase-crashlytics-ndk:18.6.2'
    implementation "com.fasterxml.jackson.core:jackson-core:2.11.1"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.11.1"
    implementation 'org.apache.commons:commons-lang3:3.14.0'
    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

buildscript {

    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files\
        classpath 'org.owasp:dependency-check-gradle:5.3.2'
    }
}

apply plugin: 'org.owasp.dependencycheck'

dependencyCheck {
    autoUpdate = true
    cveValidForHours = 4
    failBuildOnCVSS = 30
    format = 'ALL'
}
